apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply from: '../config/dependency/dependencies.gradle'

apply plugin: 'io.fabric'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "fr.bowser.time"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        resConfigs "en", "fr"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ".dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "density"

    productFlavors {
        // Dimension flavors
        universal {
            dimension "density"
        }
        mdpi {
            dimension "density"
            resConfigs "mdpi"
        }
        hdpi {
            dimension "density"
            resConfigs "hdpi"
        }
        xhdpi {
            dimension "density"
            resConfigs "xhdpi"
        }
        xxhdpi {
            dimension "density"
            resConfigs "xxhdpi"
        }
        xxxhdpi {
            dimension "density"
            resConfigs "xxxhdpi"
        }
    }

    sourceSets {
        main {
            // Split resources.
            // https://medium.com/google-developer-experts/android-project-structure-alternative-way-29ce766682f0#.sjnhetuhb
            res.srcDirs = [
                    'src/main/res/common',
                    'src/main/res/home',
                    'src/main/res/timer',
                    'src/main/res/createtimer',
                    'src/main/res/timerlist',
                    'src/main/res/notification',
                    'src/main/res/setting',
                    'src/main/res/showmode',
            ]
        }
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.20"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.20"

    // Support library
    implementation rootProject.ext.dependencies.supportConstraint
    implementation rootProject.ext.dependencies.supportDesign
    implementation rootProject.ext.dependencies.supportCardView

    // Architecture component
    implementation rootProject.ext.dependencies.room
    kapt rootProject.ext.dependencies.annotationProcessorRoom

    // Firebase
    implementation rootProject.ext.dependencies.firebaseCore

    // Other
    implementation rootProject.ext.dependencies.dagger2
    kapt rootProject.ext.dependencies.annotationProcessorDagger2

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    androidTestImplementation "android.arch.persistence.room:testing:1.0.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'

    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

}

android.applicationVariants.all { variant ->
    makeVersionCodeForVariant(variant)
    copyMappingAndApk(variant)
}

def copyMappingAndApk(variant) {
    if (variant.buildType.name == "release") {
        variant.assemble.doLast {
            def versionCode = rootProject.ext.versionCode

            // create root directory
            File flavorRootDir = new File(projectDir.getAbsolutePath()
                    + "/build/release-"
                    + versionCode + "/")
            if (!flavorRootDir.exists()) {
                flavorRootDir.mkdir()
            }

            // we remove Release word from variantName
            def variantName = variant.name.replace("Release", "")

            // copy mapping
            tasks.create("taskCopyMapping" + variantName, Copy) {
                from "build/outputs/mapping/" + variantName + "/release/mapping.txt"
                into flavorRootDir.absolutePath + "/" + variantName + "/"
            }.execute()

            // copy apk
            tasks.create("taskCopyApk" + variantName, Copy) {
                from variant.outputs[0].outputFile
                into flavorRootDir.absolutePath + "/" + variantName + "/"
            }.execute()
        }
    }
}

def makeVersionCodeForVariant(variant) {
    def dimensions = getProductFlavorsDimensions(variant.productFlavors)
    def densityNameDimension = dimensions.get("density")

    if (densityNameDimension == null) {
        throw new IllegalStateException("Missing density dimension")
    }

    def versionCode = makeVersionCode(densityNameDimension)
    variant.outputs.all {
        setVersionCodeOverride(versionCode)
    }
}

static def getProductFlavorsDimensions(productFlavors) {
    def dimensions = new HashMap<String, String>()
    productFlavors.each { productFlavor ->
        def dimension = productFlavor.getDimension()
        def name = productFlavor.getName()
        dimensions.put(dimension, name)
    }
    return dimensions
}

def makeVersionCode(String densityDimension) {
    def densityCodes = [universal: 1, mdpi: 2, hdpi: 3, xhdpi: 4, xxhdpi: 5, xxxhdpi: 6]
    def densityCode = densityCodes.get(densityDimension)
    return densityCode * 1_000_00_00 + rootProject.ext.versionCode
}

apply plugin: 'com.google.gms.google-services'